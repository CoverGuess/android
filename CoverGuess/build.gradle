apply plugin: 'android'

android {
    compileSdkVersion COMPILE_SDK_VERSION
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }

    sourceSets.main.jni.srcDirs = [] //disable automatic ndk-build call
    sourceSets {
        main {
            java {
                srcDir 'gen'
            }
        }
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:19.0.0'
    compile 'com.android.support:appcompat-v7:19.0.0'
    compile project(':lib_NineOldAndroids')
    compile fileTree(dir: 'src/main/lib', include: '*.jar')
    compile fileTree(dir: "$buildDir/native-libs", include: 'native-libs.jar')
    compile ('com.activeandroid:activeandroid:3.1-SNAPSHOT'){
            exclude module: 'support-v4'
    }
    compile ('com.squareup.retrofit:retrofit:1.6.0')
    compile ('de.greenrobot:eventbus:2.2.1')
}

task nativeLibsToJar(type: Zip, description: 'create a jar archive of the native libs') {
    destinationDir file("$buildDir/native-libs")
    baseName 'native-libs'
    extension 'jar'
    from fileTree(dir: 'src/main/libs', include: '**/*.so')
    into 'lib/'
}

tasks.withType(Compile) {
    compileTask -> compileTask.dependsOn(nativeLibsToJar)
}
